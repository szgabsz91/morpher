buildscript {
    ext {
        morpherGroupId = 'com.github.szgabsz91'
        morpherVersion = '1.6.0-SNAPSHOT'

        signingKeyId = System.getenv('SIGNING_KEY_ID')
        signingKey = new String(System.getenv('SIGNING_KEY').decodeBase64())
        signingPassword = System.getenv('SIGNING_PASSWORD')
        mavenCentralUsername = System.getenv('MAVEN_CENTRAL_USERNAME')
        mavenCentralPassword = System.getenv('MAVEN_CENTRAL_PASSWORD')

        javaVersion = '18'

        checkstyleVersion = '10.0'
        guavaVersion = '31.1-jre'
        jacocoVersion = '0.8.8'
        lombokVersion = '1.18.22'
        nexusPublishPlugin = '1.1.0'
        pmdVersion = '6.43.0'
        protobufGradlePluginVersion = '0.8.18'
        protocVersion = '3.19.4'
        publishPluginVersion = '1.1.0'
        spotbugsVersion = '4.6.0'
        spotbugsGradlePluginVersion = '5.0.6'

        combinatoricslibVersion = '3.3.3'
        commonsCollectionsVersion = '4.4'
        commonsLangVersion = '3.12.0'
        commonsTextVersion = '1.9'
        gsonVersion = '2.9.0'
        logbackVersion = '1.2.11'
        luceneCoreVersion = '9.1.0'
        protobufVersion = '3.19.0'
        slf4jVersion = '1.7.36'
        velocityVersion = '2.3'
        zip4jVersion = '2.9.1'

        assertjVersion = '3.22.0'
        junitJupiterVersion = '5.8.2'
        junitPlatformVersion = '1.8.2'
        mockitoVersion = '4.4.0'
    }

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url 'https://plugins.gradle.org/m2'
        }
    }

    dependencies {
        classpath "com.github.spotbugs.snom:spotbugs-gradle-plugin:${spotbugsGradlePluginVersion}"
        classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufGradlePluginVersion}"
        // Workaround for Spotbugs vs Protobuf:
        // - https://github.com/spotbugs/spotbugs-gradle-plugin/issues/120
        // - https://github.com/GoogleContainerTools/jib/issues/591
        classpath "com.google.guava:guava:${guavaVersion}"
        classpath "io.github.gradle-nexus:publish-plugin:${publishPluginVersion}"
    }
}

gradle.rootProject {
    apply from: "$rootDir/gradle/nexus.gradle"
}

subprojects {
    group = morpherGroupId
    version = morpherVersion

    apply from: "$rootDir/gradle/java.gradle"
    apply from: "$rootDir/gradle/lombok.gradle"
    apply from: "$rootDir/gradle/static-code-analysis.gradle"
    apply from: "$rootDir/gradle/maven.gradle"
    apply from: "$rootDir/gradle/ide.gradle"
    apply from: "$rootDir/gradle/signing.gradle"

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

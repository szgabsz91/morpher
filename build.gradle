buildscript {
    ext {
        morpherGroupId = 'com.github.szgabsz91'
        morpherVersion = '1.4.1-SNAPSHOT'

        signingKeyId = System.getenv('SIGNING_KEY_ID')
        signingKey = new String(System.getenv('SIGNING_KEY').decodeBase64())
        signingPassword = System.getenv('SIGNING_PASSWORD')
        mavenCentralUsername = System.getenv('MAVEN_CENTRAL_USERNAME')
        mavenCentralPassword = System.getenv('MAVEN_CENTRAL_PASSWORD')

        javaVersion = '16'

        checkstyleVersion = '8.41'
        guavaVersion = '30.1.1-jre'
        jacocoVersion = '0.8.6'
        nexusPublishPlugin = '1.0.0'
        pmdVersion = '6.32.0'
        protobufGradlePluginVersion = '0.8.14'
        protocVersion = '3.15.6'
        publishPluginVersion = '1.0.0'
        spotbugsVersion = '4.2.2'
        spotbugsGradlePluginVersion = '4.7.0'

        combinatoricslibVersion = '3.3.0'
        commonsCollectionsVersion = '4.4'
        commonsLangVersion = '3.12.0'
        commonsTextVersion = '1.9'
        gsonVersion = '2.8.6'
        logbackVersion = '1.2.3'
        luceneCoreVersion = '8.8.1'
        protobufVersion = '3.15.6'
        slf4jVersion = '1.7.30'
        velocityVersion = '2.3'
        zip4jVersion = '1.3.3'

        assertjVersion = '3.19.0'
        junitJupiterVersion = '5.7.1'
        junitPlatformVersion = '1.7.1'
        mockitoVersion = '3.8.0'
    }

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url 'https://plugins.gradle.org/m2'
        }
    }

    dependencies {
        classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:${spotbugsGradlePluginVersion}"
        classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufGradlePluginVersion}"
        // Workaround for Spotbugs vs Protobuf:
        // - https://github.com/spotbugs/spotbugs-gradle-plugin/issues/120
        // - https://github.com/GoogleContainerTools/jib/issues/591
        classpath "com.google.guava:guava:${guavaVersion}"
        classpath "io.github.gradle-nexus:publish-plugin:${publishPluginVersion}"
    }
}

gradle.rootProject {
    apply from: "$rootDir/gradle/nexus.gradle"
}

subprojects {
    group = morpherGroupId
    version = morpherVersion

    apply from: "$rootDir/gradle/java.gradle"
    apply from: "$rootDir/gradle/static-code-analysis.gradle"
    apply from: "$rootDir/gradle/maven.gradle"
    apply from: "$rootDir/gradle/ide.gradle"
    apply from: "$rootDir/gradle/signing.gradle"

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

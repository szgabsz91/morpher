apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

artifactory {
    contextUrl = 'https://oss.jfrog.org'

    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = 'szgabsz91'
            password = project.bintrayKey
        }
        defaults {
            publications('BintrayPublication')
            publishArtifacts = true
            publishPom = true
        }
    }

    resolve {
        repoKey = 'jcenter'
    }

    if (project.buildNumber) {
        clientConfig.info.setBuildNumber(project.buildNumber)
        clientConfig.info.setBuildName(project.name)
    }
}

bintray {
    user = 'szgabsz91'
    key = project.bintrayKey
    publications = ['BintrayPublication']
    dryRun = false
    publish = true
    override = true

    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['GPL-3.0']
        desc = project.description
        websiteUrl = project.projectUrl
        issueTrackerUrl = "${project.projectUrl}/issues"
        vcsUrl = project.projectUrl
        labels = ['morpher', 'inflection', 'analysis', 'morphology']
        githubRepo = 'szgabsz91/morpher'
        githubReleaseNotesFile = 'README.md'
        version {
            name = "${project.name}-${project.version}"
            desc = "${project.displayName} ${project.version}${project.buildNumber ? " (Build ${project.buildNumber})" : ''}"
            released  = new Date()
            vcsTag = project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = 'szgabsz91'
                password = project.mavenCentralPassword
                close = '1'
            }
        }
    }
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.description)
                root.appendNode('name', project.name)
                root.children().last() + project.pomInformation
            }
        }
    }
}
